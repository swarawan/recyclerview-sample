apply plugin: 'com.android.application'

ext.versionMajor = 1
ext.versionMinor = 2
ext.versionPatch = 3
ext.versionClassifier = null
ext.isSnapshot = true
ext.minimumSdkVersion = 15

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.swarawan.recyclerview_sample"
        minSdkVersion 15
        targetSdkVersion project.ext.minimumSdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def appName = "RecyclerSample"
            def buildType = variant.buildType.name
            def versionName = variant.versionName

            outputFileName = appName + "-" + buildType + "-" + versionName + ".apk"
        }
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 +
            ext.versionMajor * 1000000 +
            ext.versionMinor * 100000 +
            ext.versionPatch * 10000
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if(ext.versionClassifier == null) {
        if(ext.isSnapshot) {
            ext.versionClassifier = "SNAPSHOT"
        }
    }

    if(ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }

    return versionName
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:recyclerview-v7:26.1.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
